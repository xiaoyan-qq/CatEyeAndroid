buildscript {
    ext.kotlin_version="1.3.30"
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    group = 'org.mapsforge'
    version = 'master-SNAPSHOT'
    ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

    ext.androidBuildVersionTools = "28.0.0"
    ext.gdxVersion = "1.9.10"
    ext.gwtVersion = "2.8.2"
    ext.slf4jVersion = "1.7.28"
    ext.ndkVersion = "21.3.6528147"
    ext.kotlin_version="1.3.30"

    if (JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
            logging.captureStandardError LogLevel.INFO
            logging.captureStandardOutput LogLevel.INFO
        }
    }

    repositories {
        google()
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://jitpack.io" }
//        maven { url 'https://repo.boundlessgeo.com/main/' }
    }
}

static def androidCompileSdk() { return 28 }

// 14 for Support Library, 16 for sqlite-android
static def androidMinSdk() { return 23 }

static def androidTargetSdk() { return 28 }

static def versionCode() { return 1 }

def versionName() { return version }

subprojects {
    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
    project.configurations.all {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.android.support'
                    && !details.requested.name.contains('multidex')) {
                //统一版本号
                details.useVersion "29.0.2"
            }
        }
    }
}
