apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

dependencies {
    api project(':vtm')
    api "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
}

android {
    compileSdkVersion androidCompileSdk()
    buildToolsVersion "$androidBuildVersionTools"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        versionCode project.versionCode()
        versionName project.versionName()
        minSdkVersion androidMinSdk()
        targetSdkVersion androidTargetSdk()
        multiDexEnabled true
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
        }
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    lintOptions { abortOnError false }
}

android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    if (name == "debug")
        return

    def jar = project.tasks.create "jar${name.capitalize()}", Jar
    jar.dependsOn variant.javaCompileProvider
    jar.exclude '**/BuildConfig.class'
    jar.exclude '**/R.class'
    jar.exclude '**/R$*.class'
    jar.from variant.javaCompileProvider.get().destinationDir
    artifacts.add('archives', jar)

    file('natives').eachDir() { dir ->
        def nativesJar = project.tasks.create "nativesJar${name.capitalize()}-${dir.name}", Jar
        nativesJar.classifier = "natives-${dir.name}"
        nativesJar.from(dir.path)
        artifacts.add('archives', nativesJar)
    }
}

if (project.hasProperty("SONATYPE_USERNAME")) {
    afterEvaluate {
        project.apply from: "${rootProject.projectDir}/deploy.gradle"
    }
}
